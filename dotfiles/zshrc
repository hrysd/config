# use Emacs keybind
bindkey -e

setopt auto_cd

# alias
alias be='bundle exec'
alias bo='bundle open'
alias bip='bundle install --path vendor/bundle'
alias gst='git status -s'
alias ag='ag --pager="less -R"'
alias dc='docker-compose'
alias dr='docker-compose run'

# history
HISTFILE=$HOME/.zsh_history
HISTSIZE=100000
SAVEHIST=100000

setopt SHARE_HISTORY
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS

# load comletion file
fpath=(~/.zsh/completion $fpath)

autoload -U compinit
compinit -u

# vcs
autoload -Uz vcs_info

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr "+"
zstyle ':vcs_info:*' unstagedstr "!"
zstyle ':vcs_info:*' formats '(%b%c%u)'
zstyle ':vcs_info:*' actionformats '(%b|%a%c%u)'

precmd () {
  psvar=()
  LANG=en_US.UTF-8 vcs_info
  [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

src() {
  local dir

  dir="$(ghq list -p | fzf +s --tac -e --reverse)"

  if [ -n "$dir" ]; then
    cd $dir
  fi
}

vig() {
  local file

  file="$(git status -s | sed -e "s/^[MAU? ]*//g" | fzf +s --tac -e --reverse --preview 'head -50 {}' --preview-window down)"

  if [ -n "$file" ]; then
    vim $file
  fi
}

fzf_history() {
  BUFFER=$(fc -l -n 1 | awk '!a[$0]++' | fzf +s --tac -e --reverse)
  CURSOR=$#BUFFER
}

zle -N fzf_history
bindkey '^R' fzf_history

# prompt
PROMPT='%B%F{blue}[%~]%f%b%1(v|%B%F{green}%1v%f%b|)%B%F{blue}>%f%b '
RPROMPT='%B%F{blue}[%*]%f%b'

# etc
eval "$(rbenv init -)"
eval "$(direnv hook zsh)"

. $HOME/.asdf/asdf.sh
